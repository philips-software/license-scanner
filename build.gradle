buildscript {
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4.29.0"
  }
}

plugins {
    id 'application'
    id 'com.adarshr.test-logger' version '3.2.0'
    id "org.springframework.boot" version '2.5.5'
    id "org.flywaydb.flyway" version "9.1.2"
}

repositories {
    mavenLocal()
    maven { url 'https://plugins.gradle.org/m2/' }
    mavenCentral()
}

ext {
    springBootVersion = '2.5.6'
}

dependencies {
    implementation "org.springframework.boot:spring-boot-starter-web:$springBootVersion"
    implementation "org.springframework.boot:spring-boot-starter-actuator:$springBootVersion"
    implementation "org.springframework.boot:spring-boot-configuration-processor:$springBootVersion"
    implementation "org.hibernate:hibernate-validator:6.2.4.Final"
    implementation "org.springframework.boot:spring-boot-starter-data-jpa:$springBootVersion"
    implementation "com.google.code.findbugs:jsr305:3.0.2"
    implementation "com.h2database:h2:2.1.214"
    implementation "org.flywaydb:flyway-core:9.1.2"
    testImplementation "org.springframework.boot:spring-boot-starter-test:$springBootVersion"
    testImplementation "org.assertj:assertj-core:3.23.1"
    testImplementation "nl.jqno.equalsverifier:equalsverifier:3.10.1"
    testImplementation "org.mockito:mockito-core:4.6.1"
    compileOnly "pl.tlinkowski.annotation:pl.tlinkowski.annotation.basic:0.2.0"
    implementation "javax.xml.bind:jaxb-api:2.3.1"
}

testlogger {
    theme 'mocha'
}

test {
    useJUnitPlatform()
}

application {
    mainClass = 'com.philips.research.licensescanner.Application'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
}

jar {
    enabled = false
}

file("src/main/resources/application-db.properties").withReader {
   Properties props = new Properties()
   props.load(it)
   project.ext.ApplicationProps = props
}

flyway {
    url = project.ext.ApplicationProps['spring.datasource.url']
    user = project.ext.ApplicationProps['spring.datasource.username']
    password = project.ext.ApplicationProps['spring.datasource.password']
}

// we need to build classes before we can migrate
flywayMigrate.dependsOn classes

version = '0.6.0'
